<!DOCTYPE html><html xmlns='http://www.w3.org/1999/xhtml'><head><meta charset='UTF-8'/>
<title>StandardGraphIterator.ceylon</title><link href='../../.resources/favicon.ico' rel='shortcut icon'/>
<link href='../../.resources/ceylon.css' rel='stylesheet' type='text/css'/>
<link href='http://fonts.googleapis.com/css?family=Inconsolata' rel='stylesheet' type='text/css'/>
<script type='text/javascript'>var resourceBaseUrl = '../../.resources/'</script><script src='../../.resources/jquery-1.8.2.min.js' type='text/javascript'></script><script src='../../.resources/rainbow.min.js' type='text/javascript'></script><script src='../../.resources/ceylon.js' type='text/javascript'></script><script src='../../.resources/ceylondoc.js' type='text/javascript'></script></head><body><pre data-language='ceylon' style='font-family: Inconsolata, Monaco, Courier, monospace'>import ceylon.collection {
	MutableMap,
	HashMap
}

import graph {
	AdjacencyGraph
}
import graph.traversal.iterator {
	PropagatorBasedIterator
}
import graph.traversal.propagator {
	Propagator
}

by (&quot;ThorstenSeitz&quot;)
abstract class VertexColor() of white | gray | black {}
object white extends VertexColor() {}
object gray extends VertexColor() {}
object black extends VertexColor() {}

&quot;Generic traversal by using white/gray/black vertex colors and a collection of vertices waiting to be visited.
 Base for BFS (using a queue) and DFS (using a stack).&quot;
by (&quot;ThorstenSeitz&quot;)
shared abstract class StandardGraphIterator&lt;V,G,Adjacency,P&gt;(
	shared actual G graph,
	shared actual V start)
		satisfies PropagatorBasedIterator&lt;V,G,P,Adjacency&gt;
		given V satisfies Object
		given G satisfies AdjacencyGraph&lt;V&gt;
		given P satisfies Propagator&lt;V,Adjacency&gt; {

	MutableMap&lt;V,VertexColor&gt; colorMap = HashMap&lt;V,VertexColor&gt;();

	shared actual V|Finished next() {
		V? source = nextVertex();
		switch (source)
		case (null) {
			return finished;
		}
		case (is V) {
			P propagator = propagatorFor(source);
			propagator.examineVertex();
			for (Adjacency adjacency in propagator.adjacencies) {
				V target = propagator.target(adjacency);
				propagator.examineEdge(adjacency);
				if (shouldFollowVertex(target)) {
					propagator.treeEdge(adjacency);
					discoverVertex(target);
				} else {
					propagator.nonTreeEdge(adjacency);
					if (isBackEdge(target)) {
						propagator.backEdge(adjacency);
					} else {
						propagator.forwardEdge(adjacency);
					}
				}
			}
			setBlack(source);
			propagator.finishVertex();
			return source;
		}
	}

	// TODO: unsch√∂n!?
	shared default void startWith(V vertex) {
		colorMap.clear();
		discoverVertex(start);
	}

	shared formal void push(V vertex);

	shared formal V? nextVertex();

	void discoverVertex(V vertex) {
		colorMap.put(vertex, gray);
		push(vertex);
	}

	void setBlack(V vertex) {
		colorMap.put(vertex, black);
	}

	Boolean shouldFollowVertex(V vertex) {
		switch (colorMap.get(vertex))
		case (white) { return true; }
		else { return false; }
	}

	Boolean isBackEdge(V vertex) {
		switch (colorMap.get(vertex))
		case (gray) { return true; }
		else { return false; }
	}
}
</pre></body></html>