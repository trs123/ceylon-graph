<!DOCTYPE html><html xmlns='http://www.w3.org/1999/xhtml'><head><meta charset='UTF-8'/>
<title>StandardTraversal.ceylon</title><link href='../.resources/favicon.ico' rel='shortcut icon'/>
<link href='../.resources/ceylon.css' rel='stylesheet' type='text/css'/>
<link href='http://fonts.googleapis.com/css?family=Inconsolata' rel='stylesheet' type='text/css'/>
<script type='text/javascript'>var resourceBaseUrl = '../.resources/'</script><script src='../.resources/jquery-1.8.2.min.js' type='text/javascript'></script><script src='../.resources/rainbow.min.js' type='text/javascript'></script><script src='../.resources/ceylon.js' type='text/javascript'></script><script src='../.resources/ceylondoc.js' type='text/javascript'></script></head><body><pre data-language='ceylon' style='font-family: Inconsolata, Monaco, Courier, monospace'>import ceylon.collection {
	MutableMap,
	HashMap
}

import graph {
	AdjacencyGraph
}
import graph.traversal.propagator {
	Propagator
}

&quot;Generic breadth first search traversal.&quot;
by (&quot;ThorstenSeitz&quot;)
shared abstract class StandardTraversal&lt;V,G,Adjacency,P&gt;(G graph)
		satisfies PropagatorBasedTraversal&lt;V,G,P,Adjacency&gt;
		given V satisfies Object
		given G satisfies AdjacencyGraph&lt;V&gt;
		given P satisfies Propagator&lt;V,Adjacency&gt; {

	MutableMap&lt;V,VertexColor&gt; colorMap = HashMap&lt;V,VertexColor&gt;();

	shared actual V? next() {
		V? source = nextVertex();
		if (is V source) {
			P propagator = propagatorFor(source);
			propagator.visitor.examineVertex(source);
			for (Adjacency adjacency in propagator.adjacencies) {
				V target = propagator.target(adjacency);
				propagator.examineEdge(adjacency);
				if (shouldFollowVertex(target)) {
					propagator.treeEdge(adjacency);
					setGray(target);
					push(target);
				} else {
					propagator.nonTreeEdge(adjacency);
					if (isBackEdge(target)) {
						propagator.backEdge(adjacency);
					} else {
						propagator.forwardEdge(adjacency);
					}
				}
			}
			setBlack(source);
			propagator.visitor.finishVertex(source);
		}
		return source;
	}

	shared formal void push(V vertex);

	shared formal V? nextVertex();

	void setGray(V vertex) {
		colorMap.put(vertex, gray);
	}

	void setBlack(V vertex) {
		colorMap.put(vertex, black);
	}

	Boolean shouldFollowVertex(V vertex) {
		switch (colorMap.get(vertex))
		case (white) { return true; }
		else { return false; }
	}

	Boolean isBackEdge(V vertex) {
		switch (colorMap.get(vertex))
		case (gray) { return true; }
		else { return false; }
	}
}
</pre></body></html>